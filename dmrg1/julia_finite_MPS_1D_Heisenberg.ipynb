{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "8a34ca1a",
   "metadata": {},
   "source": [
    "This is a notebook for a simple DMRG algorithm: Modern MPS Version\n",
    "\n",
    "When writing the julia code shown in this notebook, I refer to Mr.Min Long's python code. \n",
    "\n",
    "https://colab.research.google.com/github/DavidGoing/DMRG/blob/main/DMRG_MPS.ipynb\n",
    "\n",
    "We take 1D Heisenberg model as an example, show the underlying code of MPS version DMRG.\n",
    "\n",
    "We DO NOT use good quantum numbers in the system."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "f267f49a",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "two_site_dmrg (generic function with 1 method)"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using Einsum\n",
    "using TensorOperations\n",
    "using KrylovKit\n",
    "using LinearAlgebra\n",
    "#initial E and F for the left and right vacuum states\n",
    "function initial_E(W)\n",
    "    E = zeros(1,size(W,1),1)\n",
    "    E[1,1,1] = 1\n",
    "    return E\n",
    "end\n",
    "function initial_F(W)\n",
    "    F = zeros(1,size(W,3),1)\n",
    "    F[1,end,1] = 1\n",
    "    return F\n",
    "end\n",
    "# MPS-MPO-MPS contract from left\n",
    "# here we use the macro: @einsum A[i, j] := B[i, k] * C[k, j]\n",
    "function contract_from_left(W,A,E,B)\n",
    "    @einsum tensor1[a,k,s,j] := E[i,a,k]*A[i,s,j]\n",
    "    @einsum tensor2[k,j,b,t] := tensor1[a,k,s,j]*W[a,s,b,t]\n",
    "    @einsum tensor3[j,b,l] := tensor2[k,j,b,t]*B[k,t,l]\n",
    "    return tensor3\n",
    "end\n",
    "# MPS-MPO-MPS contract from right\n",
    "function contract_from_right(W,A,F,B)\n",
    "    @einsum tensor1[i,s,b,l] := F[j,b,l]*A[i,s,j]\n",
    "    @einsum tensor2[i,a,t,l] := tensor1[i,s,b,l]*W[a,s,b,t]\n",
    "    @einsum tensor3[i,a,k] := tensor2[i,a,t,l]*B[k,t,l]\n",
    "    return tensor3\n",
    "end\n",
    "#construct F lib\n",
    "# MPO is the W chain\n",
    "# Alist and Blist are the MPS: the chain of A and B\n",
    "# MF is the right boundary of F\n",
    "function construct_F(Alist,MPO,Blist)\n",
    "    MF = initial_F(MPO[end])\n",
    "    F = [MF]\n",
    "    for i = length(MPO):-1:2\n",
    "        f = contract_from_right(MPO[i],Alist[i],F[1],Blist[i])\n",
    "        F = vcat([f],F)\n",
    "    end\n",
    "    return F\n",
    "end\n",
    "#construct E lib\n",
    "#ME is the left boundary of E\n",
    "function construct_E(Alist,MPO,Blist)\n",
    "    ME = initial_E(MPO[1])\n",
    "    E = [ME]\n",
    "    return E\n",
    "end\n",
    "# calculate expectation value of MPS\n",
    "function Expectation(Alist,MPO,Blist)\n",
    "    E = initial_E(MPO[1])\n",
    "    for i = 1:length(MPO)\n",
    "        E = contract_from_left(MPO[i],Alist[i],E,Blist[i])\n",
    "    end\n",
    "    return E[1,1,1]\n",
    "end\n",
    "# coarse-graining of two site MPO into one site\n",
    "function coarse_grain_MPO(W1,W2)\n",
    "    @einsum W[a,s,u,c,t,v] := W1[a,s,b,t]*W2[b,u,c,v]\n",
    "    ts = size(W) #tensor size\n",
    "    W12 = reshape(W,(ts[1],ts[2]*ts[3],ts[4],ts[5]*ts[6]))\n",
    "    return W12\n",
    "end\n",
    "# product of two MPO sites\n",
    "function product_W(W1,W2)\n",
    "    @einsum W[a,c,s,b,d,u] := W1[a,s,b,t]*W2[c,t,d,u]\n",
    "    ts = size(W) #tensor size\n",
    "    W12 = reshape(W,(ts[1]*ts[2],ts[3],ts[4]*ts[5],ts[6]))\n",
    "    return W12\n",
    "end\n",
    "# product of two Many-Body MPO \n",
    "function product_MPO(MPO1,MPO2)\n",
    "    @assert length(MPO1)==length(MPO2)\n",
    "    result = []\n",
    "    for i = 1:length(MPO1)\n",
    "        result = vcat(result,[product_W(MPO1[i],MPO2[i])])\n",
    "    end\n",
    "    return result\n",
    "end\n",
    "\n",
    "# coarse-graining of two-site MPS in to one site\n",
    "function coarse_grain_MPS(A1,A2)\n",
    "    @einsum A12[i,s,t,k] := A1[i,s,j]*A2[j,t,k]\n",
    "    ts = size(A12) #tensor size\n",
    "    A12 = reshape(A12,(ts[1],ts[2]*ts[3],ts[4]))\n",
    "    return A12\n",
    "end\n",
    "#reversive process of MPS coarse graining: SVD factorize\n",
    "function fine_grain_MPS(A,dims)\n",
    "    @assert size(A,2) == dims[1]*dims[2]\n",
    "    ts = size(A) #tensor size\n",
    "    Theta = reshape(A,(ts[1],dims[1],dims[2],ts[3]))\n",
    "    @tensor Theta[s,i,t,k] := Theta[i,s,t,k]\n",
    "    Theta = reshape(Theta,(ts[1]*dims[1],dims[2]*ts[3]))\n",
    "    F  = svd(Theta)\n",
    "    bond1 = length(F.U)/dims[1]/ts[1]\n",
    "    bond2 = length(F.Vt)/dims[2]/ts[3]\n",
    "    bond1 = convert(Int,bond1)\n",
    "    bond2 = convert(Int,bond2)\n",
    "    U = reshape(F.U,(dims[1],ts[1],bond1))\n",
    "    Vt = reshape(F.Vt,(bond2,dims[2],ts[3]))\n",
    "    S = F.S\n",
    "    return U,S,Vt\n",
    "end\n",
    "# U,S,V truncation\n",
    "function truncate_MPS(U,S,Vt,m)\n",
    "    m = min(length(S),m)\n",
    "    trunc = sum(S[m+1:end])\n",
    "    S = S[1:m]\n",
    "    U = U[:,:,1:m]\n",
    "    Vt = Vt[1:m,:,:]\n",
    "    S = S/norm(S)\n",
    "    return U,S,Vt,trunc,m\n",
    "end\n",
    "# get the ProjMPO\n",
    "function HamiltonianMultiply(E,W,F,A)\n",
    "    @einsum tensor1[j,s,a,k] := E[i,a,k]*A[i,s,j]\n",
    "    @einsum tensor2[j,b,t,k] := tensor1[j,s,a,k]*W[a,s,b,t]\n",
    "    @einsum R[k,t,l] :=tensor2[j,b,t,k]*F[j,b,l]\n",
    "    return R\n",
    "end\n",
    "\n",
    "function optimize_two_sites(A1,A2,W1,W2,E,F,m,direction)\n",
    "    W = coarse_grain_MPO(W1,W2)\n",
    "    A0 = coarse_grain_MPS(A1,A2)\n",
    "    H_eff(A) = HamiltonianMultiply(E,W,F,A)\n",
    "    Eig,AA,info = eigsolve(H_eff, A0, 1, :SR)\n",
    "    U,S,Vt = fine_grain_MPS(AA[1],[size(A1,2),size(A2,2)])\n",
    "    U,S,Vt,trunc,m = truncate_MPS(U,S,Vt,m)\n",
    "    s_diag = diagm(S)\n",
    "    if direction== \"right\"\n",
    "        @einsum A2[i,t,k] := s_diag[i,j]*Vt[j,t,k]\n",
    "        @tensor U[i,s,j] := U[s,i,j]\n",
    "        return Eig[1],U,A2,trunc,m\n",
    "    else\n",
    "        @assert direction ==\"left\"\n",
    "        @einsum A1[i,s,j] := U[s,i,m]*s_diag[m,j]\n",
    "        return Eig[1],A1,Vt,trunc,m\n",
    "    end\n",
    "end\n",
    "\n",
    "function two_site_dmrg(MPS,MPO,m,sweeps)\n",
    "    E = construct_E(MPS,MPO,MPS)\n",
    "    F = construct_F(MPS,MPO,MPS)\n",
    "    deleteat!(F,1)\n",
    "    for sweep = 1:(sweeps÷2)\n",
    "        for i = 1:length(MPS)-2\n",
    "            Energy,MPS[i],MPS[i+1],trunc,states = optimize_two_sites(MPS[i],MPS[i+1],MPO[i],MPO[i+1],E[end],F[1],m,\"right\")\n",
    "            println(\"Sweep $(sweep*2)  Sites $i $(i+1) Eenrgy $Energy states $states trunc $trunc\")\n",
    "            e = contract_from_left(MPO[i],MPS[i],E[end],conj(MPS[i]))\n",
    "            E = vcat(E,[e])\n",
    "            deleteat!(F,1)\n",
    "        end\n",
    "        for i = length(MPS)-1:-1:2\n",
    "            Energy,MPS[i],MPS[i+1],trunc,states = optimize_two_sites(MPS[i],MPS[i+1],MPO[i],MPO[i+1],E[end],F[1],m,\"left\")\n",
    "            println(\"Sweep $(sweep*2+1) Sites $i $(i+1) Energy $Energy states $states trunc $trunc\")\n",
    "            f = contract_from_right(MPO[i+1],MPS[i+1],F[1],conj(MPS[i+1]))\n",
    "            F = vcat([f],F)\n",
    "            deleteat!(E,length(E))\n",
    "        end\n",
    "    end\n",
    "    return MPS\n",
    "end\n",
    "            \n",
    "                "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "f03891b1",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "1946.2462776646785"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "d = 2 # local bond dimenstion\n",
    "N = 100 #number of sites\n",
    "D = 10 #virtue bond dimenstion\n",
    "InitialA1 = zeros(1,d,1)\n",
    "InitialA1[1,1,1] = 1\n",
    "InitialA2 = zeros(1,d,1)\n",
    "InitialA2[1,2,1] = 1\n",
    "#Initial State |010101010101>\n",
    "MPS = repeat([InitialA1,InitialA2],convert(Int,N/2))\n",
    "I = diagm([1,1])\n",
    "Z = zeros(2,2)\n",
    "Sz = [0.5 0;0 -0.5]\n",
    "Sp = [0 0; 1 0]\n",
    "Sm = [0 1; 0 0]\n",
    "# define MPO element( except the element on two boundaries)\n",
    "W = zeros(5,5,2,2) #[a,b,s,t]\n",
    "W[1,1,:,:] = I\n",
    "W[1,2,:,:] = Sz\n",
    "W[1,3,:,:] = 0.5*Sp\n",
    "W[1,4,:,:] = 0.5*Sm\n",
    "W[2,5,:,:] = Sz\n",
    "W[3,5,:,:] = Sm\n",
    "W[4,5,:,:] = Sp\n",
    "W[5,5,:,:] = I\n",
    "@tensor W[a,s,b,t] := W[a,b,s,t]\n",
    "# define the first element of MPO\n",
    "Wfirst = zeros(1,5,2,2) #[a,b,s,t]\n",
    "Wfirst[1,1,:,:] = I\n",
    "Wfirst[1,2,:,:] = Sz\n",
    "Wfirst[1,3,:,:] = 0.5*Sp\n",
    "Wfirst[1,4,:,:] = 0.5*Sm\n",
    "@tensor Wfirst[a,s,b,t] := Wfirst[a,b,s,t]\n",
    "# define the last element of MPO\n",
    "Wlast = zeros(5,1,2,2) #[a,b,s,t]\n",
    "Wlast[2,1,:,:] = Sz\n",
    "Wlast[3,1,:,:] = Sm\n",
    "Wlast[4,1,:,:] = Sp\n",
    "Wlast[5,1,:,:] = I\n",
    "@tensor Wlast[a,s,b,t] := Wlast[a,b,s,t]\n",
    "# construct MPO chain\n",
    "MPO = vcat([Wfirst],repeat([W],N-2),[Wlast]);\n",
    "HamSquared = product_MPO(MPO,MPO)\n",
    "MPS = two_site_dmrg(MPS,MPO,D,8)\n",
    "Energy = Expectation(MPS,MPO,conj(MPS))\n",
    "println(\"Final energy expectation value $Energy\")\n",
    "H2 = Expectation(MPS,HamSquared,MPS)\n",
    "println(\"variance = $(H2-Energy*Energy)\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7e191112",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "celltoolbar": "原始单元格格式",
  "kernelspec": {
   "display_name": "Julia 1.9.4",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
